import net.fabricmc.loom.task.RemapJarTask

plugins {
	id 'fabric-loom' version '0.10-SNAPSHOT'
	id 'maven-publish'
}

apply plugin: 'maven-publish'

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
version = "$project.mod_version+$project.minecraft_version"
group = project.maven_group

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	modRuntimeOnly ("net.fabricmc.fabric-api:fabric-api:${project.fabric_version}") {
		force = true //Force the version we want rather than trusting Gradle to pick it over transitive suggestions
	}
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = "UTF-8"
	it.options.release = 17
}

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = "sources"
	from sourceSets.main.allSource
}

java {
	withSourcesJar()
}

jar {
	from "LICENSE"
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			artifact(remapJar) {
				builtBy remapJar
			}
			artifact(sourcesJar) {
				builtBy remapSourcesJar
			}

			pom {
				name = 'LoadNbtBlock-Fabric'
				description = 'loads structure pieces. Mini mod for TelepathicGrunt'
				url = 'https://github.com/TelepathicGrunt/LoadNbtBlock'

				scm {
					connection = 'git:https://github.com/TelepathicGrunt/LoadNbtBlock.git'
					developerConnection = 'git:https://github.com/TelepathicGrunt/LoadNbtBlock.git'
					url = 'https://github.com/TelepathicGrunt/LoadNbtBlock'
				}

				licenses {
					license {
						name = 'MIT License'
						url = 'https://github.com/TelepathicGrunt/LoadNbtBlock'
					}
				}

				developers {
					developer {
						id = 'telepathic_grunt'
						name = 'TelepathicGrunt'
						email = 'asarnesespam@gmail.com'
					}
				}
			}

		}
	}

	repositories {
		maven {
			url = uri("https://nexus.resourcefulbees.com/repository/telepathicgrunt/")
			credentials {
				username "$System.env.MAVEN_USER"
				password "$System.env.MAVEN_PASS"
			}
		}
	}
}
